plugins {
    id 'java-library'
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'org.sonarqube' version '4.0.0.2929'
}

group 'com.apptasticsoftware'
version "${version}"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'com.apptasticsoftware:lei:3.3.0'
    testImplementation(platform('org.junit:junit-bom:5.9.1'))
    testImplementation('org.junit.jupiter:junit-jupiter:5.9.1')
    testImplementation('nl.jqno.equalsverifier:equalsverifier:3.12.3')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

ext.moduleName = 'com.apptasticsoftware.tickersymbol'

compileJava {
    options.encoding = "UTF-8"
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
        classpath = files()
    }
}

jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'Ticker Symbol'
                description = 'Java library for lookup ticker symbols.'
                url = 'https://github.com/w3stling/tickersymbol'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/w3stling/tickersymbol/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'w3stling'
                        name = 'Apptastic Software'
                        email = 'apptastic.software@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/w3stling/tickersymbol'
                    connection = 'scm:git://github.com/w3stling/tickersymbol.git'
                    developerConnection = 'scm:git:ssh://github.com/w3stling/tickersymbol.git'
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

sonarqube {
    properties {
        property "sonar.projectKey", "w3stling_tickersymbol"
        property "sonar.organization", "w3stling"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}