plugins {
    id 'java-library'
    id 'jacoco'
    id 'signing'
    id 'maven-publish'
    id 'biz.aQute.bnd.builder' version '7.2.1'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'org.sonarqube' version '7.2.2.6593'
}

group = 'com.apptasticsoftware'
version = "${version}"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.apptasticsoftware:lei:3.3.9'
    testImplementation("org.junit.jupiter:junit-jupiter:5.14.2")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    testImplementation('nl.jqno.equalsverifier:equalsverifier:4.0.7')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

ext.moduleName = 'com.apptasticsoftware.tickersymbol'

compileJava {
    options.encoding = "UTF-8"
    inputs.property('moduleName', moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}

jacoco {
    toolVersion = "0.8.14"
}

jacocoTestReport {
    reports {
        xml.required = true
        xml.outputLocation = file("${buildDir}/reports/jacoco/report.xml")
    }
}

jar {
    manifest {
        attributes(
                "Build-Jdk-Spec": java.targetCompatibility,
                "Implementation-Title": "Ticker Symbol",
                "Implementation-Version": version,
                "Specification-Title": "Ticker Symbol",
                "Specification-Version": version.replace("-SNAPSHOT", ""),
                "Automatic-Module-Name": moduleName,
                "Bundle-SymbolicName": moduleName,
                "Bundle-Description": "Java library for lookup ticker symbols",
                "Bundle-License": "https://opensource.org/licenses/MIT",
                "Bundle-Name": "Ticker Symbol",
                "Export-Package": "*;-split-package:=merge-first;-noimport:=true",
        )
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'Ticker Symbol'
                description = 'Java library for lookup ticker symbols.'
                url = 'https://github.com/w3stling/tickersymbol'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/w3stling/tickersymbol/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'w3stling'
                        name = 'Apptastic Software'
                        email = 'apptastic.software@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/w3stling/tickersymbol'
                    connection = 'scm:git://github.com/w3stling/tickersymbol.git'
                    developerConnection = 'scm:git:ssh://github.com/w3stling/tickersymbol.git'
                }
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

sonar {
    properties {
        property "sonar.projectKey", "w3stling_tickersymbol"
        property "sonar.organization", "w3stling"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}